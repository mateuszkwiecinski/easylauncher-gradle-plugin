apply plugin: 'com.starter.application.android'
apply plugin: 'com.starter.easylauncher'

android {
    defaultConfig {
        minSdkVersion 26
    }
    buildTypes {
        named("release") {
            debuggable false
            signingConfig signingConfigs.debug
        }
    }
    flavorDimensions "reportedBugs"
    productFlavors {
        register("twoVectors") {
            dimension "reportedBugs"
        }
        register("wrongLabelPosition") {
            dimension "reportedBugs"
        }
        register("chromeLike") {
            dimension "reportedBugs"
        }
        register("chromeLikeCustomized") {
            dimension "reportedBugs"
        }
        register("chromeLikeTop") {
            dimension "reportedBugs"
        }
        register("alpha") {
            dimension "reportedBugs"
        }
        register("customFont") {
            dimension "reportedBugs"
        }
        register("customFontByFile") {
            dimension "reportedBugs"
        }
        register("customFontByOtf") {
            dimension "reportedBugs"
        }
        register("usingAndroidVersion") {
            dimension "reportedBugs"
            applicationIdSuffix = ".app.dev"
            versionName = "1.0.4"
        }
    }
}

dependencies {
    implementation project(":adaptive-support")
}

easylauncher {
    buildTypes {
        release {
            enable false
        }
    }
    productFlavors {
        chromeLike {
            filters chromeLike("Beta")
        }
        chromeLikeCustomized {
            filters chromeLike(label: "GDPR", ribbonColor: "#723D46", labelColor: "#EEFFFEE", labelPadding: 25, overlayHeight: 0.6, textSizeRatio: 0.15)
        }
        chromeLikeTop {
            filters chromeLike(label: "Top", gravity: "TOP"), chromeLike(label: "Bottom", gravity: "BOTTOM")
        }
        twoVectors {
            filters grayRibbonFilter(),
                    customRibbon(label: "second", ribbonColor: "#6600CC", labelColor: "#FFFFFF", position: "bottom")
        }
        wrongLabelPosition {
            filters customRibbon(label: "GE", ribbonColor: "#cc0000", position: "top", textSizeRatio: 0.1f),
                    customRibbon(label: "QA-WHI", ribbonColor: "#0000cc", labelColor: "#FFFFFF", position: "bottom", textSizeRatio: 0.1f)
        }
        alpha {
            filters customRibbon(ribbonColor: "#99000000", labelColor: "#88FF0000", textSizeRatio: 0.3f)
        }
        customFont {
            filters chromeLike(label: "bold", font: "ComicSansMS-Bold"),
                    chromeLike(label: "TOP", gravity: "TOP"),
                    customRibbon(label: "bottom", font: "ComicSansMS", position: "bottom"),
                    customRibbon(label: "topLeft", font: "ComicSansMS", position: "topLeft"),
                    customRibbon(label: "topRight", font: "ComicSansMS", position: "topRight"),
                    customRibbon(label: "regular", font: "ComicSansMS", position: "top"),
                    overlayFilter(new File("example-custom/launcherOverlay/beta.png"))
        }
        customFontByFile {
            filters customRibbon(label: "\ue868\ue3B7", font: file("fonts/MaterialIcons-Regular.ttf"), position: "top"),
                    chromeLike(label: "\ue857\ue8DC", font: file("fonts/MaterialIcons-Regular.ttf"))
        }
        customFontByOtf {
            filters customRibbon(label: "condensed", ribbonColor: "#99000000", font: file("fonts/StintUltraCondensed-Regular.otf"), position: "top"),
                    chromeLike(label: "otf font", font: file("fonts/StintUltraCondensed-Regular.otf"))
        }
        register("usingAndroidVersion") {
            filters customRibbon(label: "DEV ${android.productFlavors.usingAndroidVersion.versionName}")
        }
    }
}
